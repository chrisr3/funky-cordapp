import org.jetbrains.kotlin.gradle.tasks.KotlinCompile
import static org.gradle.jvm.toolchain.JavaLanguageVersion.of

plugins {
    id 'net.corda.cordapp.cordapp-configuration'
    id 'org.jetbrains.kotlin.jvm' apply false
}

subprojects {
    group = 'com.example.funky'
    version = funkyVersion

    pluginManager.withPlugin('java') {
        java {
            testing {
                suites {
                    test {
                        useJUnitJupiter(junitVersion)
                    }
                }
            }
        }
    }

    pluginManager.withPlugin('org.jetbrains.kotlin.jvm') {
        kotlin {
            jvmToolchain {
                languageVersion = of(11)
            }
        }
    }

    pluginManager.withPlugin('org.jetbrains.kotlin.plugin.allopen') {
        allOpen {
            annotations(
                'javax.persistence.Entity',
                'javax.persistence.Embeddable',
                'javax.persistence.MappedSuperclass'
            )
        }
    }

    pluginManager.withPlugin('maven-publish') {
        tasks.register('install') {
            dependsOn 'publishToMavenLocal'
        }
    }

    configurations {
        all {
            resolutionStrategy {
                // FORCE Gradle to use latest SNAPSHOT versions.
                cacheChangingModulesFor 0, 'seconds'
            }
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
    }

    tasks.withType(KotlinCompile).configureEach {
        kotlinOptions {
            languageVersion = '1.7'
            apiVersion = '1.7'
            verbose = true
            freeCompilerArgs += [
                '-Xjvm-default=all',
                '-java-parameters'
            ]
        }
    }

    tasks.withType(Test).configureEach {
        doFirst {
            systemProperty 'java.io.tmpdir', buildDir.absolutePath
        }
    }

    tasks.withType(GenerateModuleMetadata).configureEach {
        enabled = false
    }
}

wrapper {
    gradleVersion = '7.4.2'
}
